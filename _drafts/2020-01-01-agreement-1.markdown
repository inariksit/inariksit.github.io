---
layout: post
title:  "Generalising agreement: part I"
date:   2010-01-01
categories: gf
tags: "gf linguistics"
---

Lately I've been thinking about verbal agreement in various GF resource grammars.

This will be a series of three posts. (I publish this plan so that I will feel the pressure to actually write them.)
The first post---this one---is an introduction to verbal agreement, most useful for a reader with little knowledge in linguistics or GF. The second post will be about implicit arguments: how do we model the real world adding all kinds of inconvenient restrictions on how we speak. In the third post I will talk about obliques and how, if you squint enough, you can kind of interpret preposition contraction and other unrelated things as agreement!

If you're a seasoned GF grammarian and have any familiarity with linguistics, the first post won't be all that new information, but you surely wouldn't want to miss my hot takes on ergativity. If you don't know what ergativity means, that's a sign that you need to read the first post.


## Agreement in linguistics

Verbal agreement means that one or more of the verb's arguments are marked in the verb form. If you're not familiar with other languages than Standard Average European, you've probably only seen subject agreement:

 &nbsp;          |1st person singular | 3rd person singular
-----------------|----------------|---------------------------
**Intransitive** |<font color="green">I</font> sleep-<strong><font color="green">∅</font></strong>       | <font color="green">the cat</font> sleep<strong><font color="green">s</font></strong>
**Transitive**   |<font color="green">I</font> drink-<strong><font color="green">∅</font></strong> water | <font color="green">the cat</font> drink<strong><font color="green">s</font></strong> water
-----------------|----------------|---------------------------

Both verbs, *sleep* and *drink*, only show marking for the subject: null morpheme (marked as -∅) for *I*, and -s for *the cat*. *Water*, which is the object of *drink*, doesn't contribute to the conjugation. Contrast this with Hungarian [[Wikipedia](https://en.wikipedia.org/wiki/Hungarian_verbs#Definite_and_indefinite_conjugations)]:

<img src="/images/hungarian-agreement.png" alt="Screenshot of Wikipedia. The text reads: In Hungarian, verbs not only show agreement with their subjects but also carry information on the definiteness of their direct objects. This results in two types of conjugations: definite (used if there is a definite object) and indefinite (if there is no definite object" />

In fact, multiple agreement is pretty common in languages around the world. The map below is taken from [World Atlas of Language Structures](https://wals.info/) (WALS): red dot means "like English", orange dot means "like Hungarian".

<a id="person-marking-wals" href="https://wals.info/feature/102A?v3=cff0&v1=cfff&v0=cfff&v2=cd00&v4=cffc&v5=cfc0#2/30.8/170.6"><img src="/images/person-marking-wals.png" /></a>

<!-- > Verbal person marking of just the A [agent] is most common in Eurasia. It is characteristic of most of the Indo-European languages of Europe, the Uralic languages of Russia, Finland, Estonia and Hungary, the Turkic languages, and the Dravidian languages of India. It is also found in eastern Africa, especially among the Afro-Asiatic languages, in the north of South America and in New Guinea. By contrast, it is extremely rare in North America and there is no instance of it among the languages in the sample in Australia. Verbal person marking of just the P [patient] is an infrequent phenomenon everywhere. -->

Now I'll present different options for argument marking, and how to do them in GF.

For simplicity, I'm only showing positive present indicative forms. You can freely assume, in addition to person marking, some form of tense, aspect, mood, polarity etc.

## No person marking

The simplest person marking is no person marking. Let's start easy and do Chinese:

```haskell
lincat
  V,V2 = {s : Str} ;

lin
  sleep_V  = {s = "睡觉"} ;

  drink_V2 = {s = "喝"} ;
```

## 1-dimensional person marking

Next possibility, the verb can mark one of its core arguments, subject or object.

### Subject marking

Let's use English as an example of subject marking:

```haskell
param
  Agr = Sg3 | Other ;

lincat
  V,V2 = {s : Agr => Str} ;

lin
  sleep_V  = {s = table { Sg3   => "sleeps" ;
                          Other => "sleep" }} ;

  drink_V2 = {s = table { Sg3   => "drinks" ;
                          Other => "drink" }} ;
```

So far we have only created an inflection table---nothing in the code says that we have *subject* marking, just that we have one-dimensional table. But when we add lincats and linearisations for `VP` and `Cl`, it will become apparent which argument the verb marks. The following GF code models English (and other red dots on the WALS map):

#### GF grammar for English

```haskell
-- English
lincat
  V,
  V2,
  VP = {s : Agr => Str} ;
  NP = {s : Str ; a : Agr} ;
  Cl = {s : Str} ;

lin
  -- UseV : V -> VP
  UseV v = v ;

  -- ComplV2 : V2 -> NP -> VP
  ComplV2 v2 np = {s = \\agr => v2.s ! agr ++ np.s} ;

  -- PredVP : NP -> VP -> Cl
  PredVP np vp = {s = np.s ++ vp.s ! np.a} ;
```

### Object marking

Now, let's implement the missing categories and functions for a language that marks only object, not subject.  According to WALS, this is quite rare but not unheard of; 24 languages of the sample of 378 do that. (They are shown as yellow dots on [the previous map](#person-marking-wals).)

To keep it simple, let's assume that there is a language just like English but object marking.

|Transitive         |Sg1 subject | Sg3 subject  |
|:-----------------:|:----------|:--------------------------|
|**Sg1 object**   |I drink-<strong><font color="purple">∅</font></strong> <font color="purple">me</font> | the cat drink-<strong><font color="purple">∅</font></strong> <font color="purple">me</font> |
|**Sg3 object**   |I drink<strong><font color="purple">s</font></strong> <font color="purple">water</font> | the cat drink<strong><font color="purple">s</font></strong> <font color="purple">water</font> |
|-----------------|----------------|---------------------------|

You see, "I drink<strong>s</strong> water" would be ungrammatigal in ordinary English, where verbs mark the subject. In contrast, Like-English-but-object-marking puts the -s in *I drinks water* because of *water*, not because of *I*.

Now, the WALS data doesn't tell me what do these 24 languages mark in an intransitive verb, so I decide that in Like-English-but-object-marking, intransitives have no person marking. (There are other options---more on those later!)


| Intransitive    |Sg1 subject | Sg3 subject |
|:---------------:|:----------|:--------------------------|
| &nbsp;          | I sleep-<strong>∅</strong> | the cat sleep-<strong>∅</strong> |
|-----------------|----------------|---------------------------|

#### GF grammar for Like-English-but-object-marking

```haskell
-- Like English but object-marking
lincat
  V, VP = {s : Str} ;
  V2 = {s : Agr => Str} ;
  NP = {s : Str ; a : Agr} ;
  Cl = {s : Str} ;

lin
  -- UseV : V -> VP
  UseV v = v ;

  -- ComplV2 : V2 -> NP -> VP
  ComplV2 v2 np = {s = v2.s ! np.a ++ np.s} ;

  -- PredVP : NP -> VP -> Cl
  PredVP np vp = {s = np.s ++ vp.s} ;
```

Let's look at the lexical categories, `V` and `V2`. The easy case first: `V2` is still an inflection table `Agr => Str`, only this time `Agr` marks the object. In contrast, the lincat for `V` doesn't need a table, just a string!

This change is reflected in `VP`. It's clear when you look how to construct a VP: either from a `V`, which never inflected, or from a `V2` and a object---just what is needed to pick out the correct form from the `V2`. In turn, `PredVP` is just trivial concatenation, because the subject doesn't contribute anything to the verb inflection.

<!--
In English, `VP` needs to retain its potential for agreement, and only `PredVP` can pick out a string, because that's when the subject is added. In Like-English-but-…, we get rid of the incertainty already at `ComplV2`, and adding subject is just trivial concatenation. -->

<!-- Compare `ComplV2` and `PredVP` in English and Like-English-but-object-marking:

```haskell
-- English
ComplV2 v2 np = {s = \\agr => v2 ! agr ++ np.s} ;

-- Like-English-but-…
ComplV2 v2 np = {s = v2.s ! agr ++ np.s} ;
```

```haskell
-- English
PredVP np vp = {s = np.s ++ vp.s ! np.a} ;

-- Like-English-but-…
PredVP np vp = {s = np.s ++ vp.s} ;
```

Pretty cool right? -->


### Object marking: another option for intransitive verbs

Previously we just had zero marking for intransitive verbs. But there is another strategy that appears in some languages[^1]:
* For transitive verbs, mark the object in the verb.
* For intransitive verbs, mark the subject *in the same way* the object is marked for transitive verbs.

(There is a name for this and I will introduce it soon. Let's just finish our Like-English-but-… series!)

We established already how transitive verbs work:

 |Transitive       |Sg1 subject         | Sg3 subject                     |
 |:---------------:|:-------------------|:--------------------------------|
 |**Sg1 object**   |I drink-<strong><font color="purple">∅</font></strong> <font color="purple">me</font>    | the cat drink-<strong><font color="purple">∅</font></strong> <font color="purple">me</font>          |
 |**Sg3 object**   |I drink<strong><font color="purple">s</font></strong> <font color="purple">water</font> | the cat drink<strong><font color="purple">s</font></strong> <font color="purple">water</font>     |
 |-----------------|--------------------|---------------------------------|

Now let's add intransitive verbs. As you can see, a transitive verb is marked with an __-s__, when its object is a 3rd person singular NP. Logically then, an intransitive verb will be marked with __-s__, when its *subject* is a 3rd person singular NP.

The same logic holds for 1st person. A transitive verb is unmarked, when its object is a 1st person singular NP (*me*). When *me* is a subject of an intransitive verb like *sleep*, then that verb should also be unmarked.

Yes, the subject case for an intransitive verb is now *me*---*I* is reserved for transitive verbs.


 | Intransitive    | Sg1 subject   | Sg3 subject         |
 |:---------------:|:--------------|:--------------------|
 | &nbsp;          | <font color="purple">me</font> sleep-<strong><font color="purple">∅</font></strong> | <font color="purple">the cat</font> sleep<strong><font color="purple">s</font></strong> |
 |-----------------|---------------|---------------------|

However, I have ignored NP case so far in my GF examples, and I continue ignoring it. (Apologies if you got excited about novel subject cases!) One thing at a time, and now we're looking at verbal agreement.


<!-- Now, in this post I'm mostly concerned about verbal agreement, but for a proper "mark the subject in the same way as the object" strategy, we should also keep in mind the case marking. In a typical language that uses such a strategy, *I* is reserved only for the doer of a transitive verb, and *me* is both the object of a transitive verb and the subject of an intransitive verb. So this looks more consistent:

| Intransitive    | Sg1 subject    | Sg3 subject         |
|:---------------:|:---------------|:--------------------|
| &nbsp;          | me sleep-<strong><font color="green">∅</font></strong> | the cat sleep-<strong><font color="purple">s</font></strong> |
|-----------------|----------------|---------------------| -->

#### GF grammar for Like-English-but-object-marking-ERG

I realise that the names of my invented languages are getting quite unwieldy. So let's just call them LEBOM (Like-English-but-object-marking) and LEBOM-ERG, for reasons that will become obvious soon.

```haskell
-- LEBOM-ERG
lincat
  V, V2, VP = {s : Agr => Str} ;
  NP = {s : Str ; a : Agr} ;
  Cl = {s : Str} ;

lin
  -- UseV : V -> VP
  UseV v = v ;

  -- ComplV2 : V2 -> NP -> VP
  ComplV2 v2 np = {s = \\_ => v2.s ! agr ++ np.s} ;

  -- PredVP : NP -> VP -> Cl
  PredVP np vp = {s = np.s ++ vp.s ! np.a} ;
```

We're back to `{s : Agr => Str}` for all verbal categories. Unlike LEBOM, where intransitive verbs had always zero marking, in LEBOM-ERG all verbs have some marking, it just varies *for which argument*. So both `V` and `V2` need an inflection table.

Because `V` needs a subject to know the correct form, `VP` needs to be an inflection table too. Just looking at `UseV` and `PredVP`, they are exactly like in [ordinary English](#gf-grammar-for-english).

In contrast, `ComplV2` has a different behaviour: it picks out the right form of the `V2` already when forming a `VP`, using the object's agreement. But `VP` still needs a table, that's how GF works. So we put the chosen form in a dummy inflection table: all left-hand sides lead to that form.

### Subject *or* object: Differential argument marking

Who says you need to do your argument marking based on its *role*? Instead, you can mark based on something inherent in the argument.

For instance, if a transitive verb has one animate and one inanimate argument, mark the verb based on the animate argument, regardless or their roles. In the following examples, the verb *write* agrees with its animate argument whenever it has one.

|  Animate > Inanimate      |Sg1 subject            | Sg3 subject                     |
|:---------------:|:----------------------|:--------------------------------|
|**Sg1 object**   |<font color="999898">I write-∅ me</font>       | the blog write-<strong><font color="purple">∅</font></strong> <font color="purple">me</font>         |
|**Sg3 object**   |<font color="green">I</font>  write-<strong><font color="green">∅</font></strong> the blog | <font color="999898">the blog write<b>s</b> the blog</font>   |


If there are two animate arguments, they can be ordered by person hierarchy, such as 1st > 2nd > 3rd. Here's an example, where *love* agrees with the argument that is highest in the person hierarchy:

| P1 > P2 > P3    | P2 subject         | P3 subject          |
|:---------------:|:-------------------|:--------------------|
|**P2 object**    |<font color="999898">you love-<b>∅</b> you</font>  | he love-<strong><font color="purple">∅</font></strong> <font color="purple">you</font>    |
|**P3 object**    |<font color="green">you</font> love-<strong><font color="green">∅</font></strong> him  | <font color="999898">he love<b>s</b> him</font>   |


Now, I think you've seen enough GF code that you should be able to do this on your own!

I have created a repository at [github.com/inariksit/gf-agreement-tutorial](https://github.com/inariksit/gf-agreement-tutorial), which contains examples to this post and the following posts.

In the directory [like-english-but](https://github.com/inariksit/gf-agreement-tutorial/tree/master/like-english-but#readme),
you find a complete abstract syntax and finished concrete syntaxes for English, LEBOM and LEBOM-ERG. However, Like-English-but-differential-argument-marking (LEBDAM) is just a dummy concrete syntax, so you have a chance to implement it properly.

Some instructions:

* Intransitive verbs mark their only argument.
* You will need to modify the `NP` category (and obviously all verbal categories). However, you can still ignore case in NP: *you love he* and *the blog write I* are acceptable output, if you only want to concentrate on person marking in the verb.
* You don't need to use animacy or person hierarchy---you can make your system as wild as you want, as long as you can encode it in GF.

If you want feedback, make your solution available to me! You can make a pull request on GitHub or send your code to [inari.listenmaa@gmail.com](mailto:inari.listenmaa@gmail.com).

<!-- This offer does not expire; as long as this paragraph is still online, I'll be happy to get answers. -->

_____

And we're done with 1-dimensional agreement! If I have missed something, please let me know. If it's about, say, case or clitics or adpositions or word order or wearing a cowboy hat to mark an argument, I'll be even more interested in it for the 3rd post.

Soon we will go on to 2-dimensional agreement. But before that, I need to introduce properly "the other strategy" for the alignment of verbal person marking, which was used in LEBOM-ERG.

## Interlude: Nominative-accusative vs. Absolutive-ergative

So far I've used the term *subject* for two things: the only argument of an intransitive verb (*__subject__ sleeps*), and the doer argument of a transitive verb (*__subject__ eats object*).

Now, let's make the definitions a bit narrower. With __<font color="green">subject</font>__, we refer to the only argument of an intransitive verb, whereas transitive verbs have an __<font color="blue">agent</font>__ and a __<font color="purple">patient</font>__. Like this:

<ul>
<li><span title="SUBJECT"><strong><font color="green">the cat</font></strong></span>:S sleeps</li>
<li><span title="AGENT"><strong><font color="blue">the cat</font></strong></span>:A eats <span title="PATIENT (or OBJECT)"><strong><font color="purple">the mouse</font></strong></span>:P</li>
</ul>

____

*How about the word "object"? Doesn't matter all that much: __<font color="purple">agent</font>__ vs. __<font color="green">subject</font>__ is an important distinction regarding argument marking, but "object" and "patient" can be used interchangeably for the non-agent argument of a transitive verb. For (what I hope to be) clarity, I'll use "patient" in contexts where subject/agent distinction is important, and "object" when it doesn't matter. The scope of this decision is this series about agreement; in other posts other rules apply.*

____

With these 3 roles, we have 4 strategies of marking them all[^2]:
<ol>
  <li>All same:
    <ul>
    <li><span title="SUBJECT"><strong><font color="green">subject</font></strong></span> sleep</li>
    <li><span title="AGENT"><strong><font color="green">agent</font></strong></span> eat <span title="PATIENT (or OBJECT)"><strong><font color="green">patient</font></strong></span></li>
    </ul>
  </li>
  <li>Subject and agent same:
    <ul>
    <li><span title="SUBJECT"><strong><font color="green">subject</font></strong></span> sleeps</li>
    <li><span title="AGENT"><strong><font color="green">agent</font></strong></span> eats <span title="PATIENT (or OBJECT)"><strong><font color="purple">patient</font></strong></span></li>
    </ul>
  </li>
  <li>Subject and patient same:
    <ul>
    <li><span title="SUBJECT"><strong><font color="purple">subject</font></strong></span> sleeps</li>
    <li><span title="AGENT"><strong><font color="green">agent</font></strong></span> eats <span title="PATIENT (or OBJECT)"><strong><font color="purple">patient</font></strong></span></li>
    </ul>
  </li>
  <li>All different:
    <ul>
    <li><span title="SUBJECT"><strong><font color="green">subject</font></strong></span> sleeps</li>
    <li><span title="AGENT"><strong><font color="blue">agent</font></strong></span> eats <span title="PATIENT (or OBJECT)"><strong><font color="purple">patient</font></strong></span></li>
    </ul>
  </li>
</ol>

<img src="/images/nom-acc-common-features.png" />
<img src="/images/abs-erg-common-features.png" />

__TODO: explain nominative-accusative vs. absolutive-ergative.__

Some terminology notes follow. I personally think that the terminology is unnecessarily confusing---all these terms conflate a lot of features, whereas in the real world, they can appear in all kinds of combinations.

* The short name for "the strategy of marking subject and patient with absolutive case, and agent with ergative case" is called *ergativity*.
* Such a language is called an *ergative* language.
* A language can be ergative even if it doesn't have case marking. All examples in this post have just used verb inflection. Even if the last example didn't switch the case to "me sleep" but was "I sleep", I'd argue that the language is still pretty ergative in terms of verb inflection.
* Well, it doesn't even need to be in verb inflection, it can be encoded in word order, or through adpositions, or by the speaker wearing a cowboy hat when mentioning the agent, and a pirate hat when mentioning the subject and the patient. The bottom line is: does the language treat subject and patient the same, and agent in another way? If yes, then we have an "ergative" language.


* A bit more exact names are *absolutive-ergative system* and *nominative-accusative system*. Absolutive and nominative are usually the unmarked cases in their respective systems, ergative and accusative are the marked cases. *Nominative-accusative* is such a well-established order, so *absolutive-ergative* follows the pattern.
* For the record, there are also nominative-accusative languages with a marked nominative. You might see them called "nominative-absolutive" (or maybe I'm just reading overly confusing grammars at my spare time.) But if we're just interested in alignment, the relevant test is to compare agents and subjects: are they both marked in *the same way*? If yes, then you have a nominative-accusative system where nominative is the marked one and accusative is the unmarked one.
* The same goes if you see a combination like "ergative-genitive".



## 2-dimensional marking


Next, let's do Hungarian (within the sample shown previously, that is, ignoring the 2nd person object suffix).
Hungarian is nominative-accusative, so for consistency I'm talking about subjects and objects again.

But now that we have 2-dimensional agreement, it stops being relevant for verbal agreement whether we have absolutive-ergative or nominative-accusative system. When we only had to mark one

```haskell
param
  Subject = Sg1 | Sg2 | Sg3 | Pl1 | Pl2 | Pl3 ;
  Object = DefObj | IndefObj ;

lincat
  V  = {s :           Subject => Str} ;
  V2 = {s : Object => Subject => Str} ;

lin
  sleep_V = {s =
    table { Sg1 => "..." ;
                    ...
                    Pl3 => "..." }
  } ;
  drink_V2 = {s =
    table { DefObj   => table { Sg1 => "..." ;
                                ...
                                Pl3 => "..." } ;
            IndefObj => table { Sg1 => "..." ;
                                ...
                                Pl3 => "..." }
          }
  } ;
```

### 3-dimensional marking

And why stop at direct object? If you've read my blog before, you knew this was coming---Basque ditransitive auxiliary agrees with agent, patient and indirect object.

Basque happens to be ergative, so we talk about subject, agent and patient again.


The patient agreement in transitive verbs covers all persons and numbers, but in ditransitive, it's restricted to singular vs. plural. (If someone who knows Basque is reading this and wondering, I'm ignoring second person familiar forms and allocutive for now, but I will introduce them in the next post.) Within these restrictions, we can model Basque verbs as follows:

```haskell
param
  Agr = Sg1 | Sg2 | Sg3 | Pl1 | Pl2 | Pl3 ;
  DtPat = SgPat | PlPat ;

lincat
  V  = {s :                 Agr => Str } ;
  V2 = {s :          Agr => Agr => Str } ;
  V3 = {s : DtPat => Agr => Agr => Str } ;
```

Let's start from the order of the arguments.

Also, the fact that Basque is absolutive-ergative is actually not at all interesting from verb inflection perspective. We need to mark both, so the argument marking strategy doesn't bring anything new to the verbs, unlike Like-English-but-patient-marking-and-ergative, which was an exciting and novel way of inflecting English verbs.

We do notice ergativity in Basque in nominal cases.

```haskell
  sleep : IntransVerb =
    table { Sg1 => "..." ;
            ...
            Pl3 => "..." } ;

  drink : TransVerb =
    table { Def   => table { Sg1 => "..." ;
                             ...
                             Pl3 => "..." } ;
            Indef => table { Sg1 => "..." ;
                             ...
                             Pl3 => "..." }
    } ;
```

## Read more

* The whole WALS article about person marking: [wals.info/chapter/102](https://wals.info/chapter/102)

## Footnotes

[^1]: In case you're wondering what "some languages" means, here's a WALS map. Pink shapes are different varieties of the strategy *mark transitive object same as intransitive subject* (at least some of the time).<a href="https://wals.info/feature/100A?v1=dffc&v0=dfff&v5=tf6f&v3=ff6f&v2=cfff&v4=sf6f&v6=cf6f#2/16.6/148.9"><img src="/images/alignments-wals.png" alt="Another world map from WALS" /></a>

[^2]: Technically there's a 5th alternative; subject is different but agent and patient are the same. But it would be strange to spend precious morphemes on just the case where it doesn't make a difference (intransitive subject vs. both transitive arguments) and then not spend them where it is actually important---one would want to know whether <font color="green">you</font> see <font color="purple">me</font> or <font color="green">I</font> see <font color="purple">you</font>.
